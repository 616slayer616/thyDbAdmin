plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'jacoco'
    id 'org.sonarqube' version '4.2.1.3168'
    id 'org.springframework.boot' version '3.1.1'
    id 'io.spring.dependency-management' version '1.1.0'
}

group 'org.padler'
version '1.4.0'

sourceCompatibility = 19

test {
    useJUnitPlatform()
}

test.finalizedBy jacocoTestReport

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-data-jpa')

    // Test
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.springframework.security:spring-security-test')
    testImplementation('org.springframework.boot:spring-boot-starter-validation')
    testImplementation('org.postgresql:postgresql')
    testImplementation('org.testcontainers:postgresql:1.18.3')

    testImplementation('org.junit.jupiter:junit-jupiter-engine')
    testImplementation('org.mockito:mockito-junit-jupiter')
}

sonarqube {
    properties {
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.organization", "616slayer616-github"
        property "sonar.projectName", "thyDbAdmin"
        property "sonar.projectKey", "616slayer616_thyDbAdmin"
        property "sonar.coverage.jacoco.xmlReportPaths", "$buildDir/reports/jacoco/test/jacocoTestReport.xml"
    }
}

bootJar {
    enabled = false
}

jar {
    enabled = true
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'thydbadmin'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'thyDbAdmin'
                description = 'Thymeleaf DB admininstration'
                url = 'http://github.com/616slayer616/thyDbAdmin'
                licenses {
                    license {
                        name = 'License'
                        url = 'https://github.com/616slayer616/thyDbAdmin/blob/master/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = '616slayer616'
                        name = 'Patrick Adler'
                        email = 'patrick@padler.org'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/616slayer616/thyDbAdmin.git'
                    developerConnection = 'scm:git:ssh://github.com/616slayer616/thyDbAdmin.git'
                    url = 'http://github.com/616slayer616/thyDbAdmin'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = System.getenv("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/616slayer616/thyDbAdmin"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
